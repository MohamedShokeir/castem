************************************
* Test Traction 3D
************************************

OPTI ECHO 1 DIME 3 ELEM CU20;
OPTI MODE TRID;
OPTI EPSI UTILISATEUR;
* important pour MFront : OPTI EPSI UTILISATEUR;

************************************
* Maillage
************************************

P1 = 0. 0. 0.;
P2 = 1. 0. 0.;
P3 = 1. 1. 0.;
P4 = 0. 1. 0.;
D1 = P1 DROIT 1 P2;
D2 = P2 DROIT 1 P3;
D3 = P3 DROIT 1 P4;
D4 = P4 DROIT 1 P1;
CONT1 = D1 ET D2 ET D3 ET D4;
S1 = SURF CONT1 PLAN;
X1 = 0. 0. 1.;
V1 = S1 VOLU TRAN 1 X1;

*TRACE V1;

************************************
* Propriétés mécaniques
************************************

* TT = -150.;
* RR0 = (430. '+' (10.*(EXP(-0.019*TT))));
* RRL = (461. '+' (28.*(EXP(-0.016*TT))));
* OPTI SORT SIM_A81_P20;
* YO1 = (197600-(TT*118.8));
* KK0 = MAXI((PROG (80.-(0.5*TT)) 0.));

CMAT = 'MOTS' 'YOUN' 'NU' 'RHO' 'phi' 'fN'
'fc' 'fr' 'q1' 'q2' 'An0' 'ps' 'sigseuil';

*PLOI = 'MOTS' 'T';

CVAR = 'MOTS' 'EEXX' 'EEYY' 'EEZZ' 'EEXY' 'EEXZ' 'EEYZ' 'p' 'f'
'ssta' 'fsta' 'fn' 'fg' 'brok';

MOD1  = 'MODELISER' V1 'MECANIQUE' 'ELASTIQUE' 'NON_LINEAIRE'
'UTILISATEUR' 'LIB_LOI' 'src/libUmatBehaviour.so' 'FCT_LOI'
'umatgtn_malls' 'C_MATERIAU' CMAT 'C_VARINTER' CVAR ;

MAT1 = MATE MOD1 'YOUN' 70.e3 'NU' 0.3 'RHO' 0. 'phi' 0.
'fN' 0.0215 'fc' 0.04  'fr' 0.056  'q1' 1.5 'q2' 2.0
'An0' 2.92 'ps' 0.0321 'sigseuil' 500. ;

************************************
* Conditions aux limites
************************************

P5  = 1. 0. 1.;
P6  = 1. 1. 1.;
P7  = 0. 1. 1.;
PM  = (1/2.) (1/2.) (1/2.);
SD  = V1 POIN PLAN P2 P3 P5 0.0001;
SD2 = V1 POIN PLAN P1 P4 P7 0.0001;
SF  = V1 POIN PLAN P1 P2 P5 0.0001;
SF2 = V1 POIN PLAN P3 P6 P7 0.0001;
SH  = V1 POIN PLAN P5 P6 P7 0.0001;

CL1 = BLOQ S1 UZ;
CL2 = BLOQ SF UY;
CL3 = BLOQ SD UX;
CL4 = BLOQ SH UZ;

REL1 = RELA ENSE UX SD2;
REL2 = RELA ENSE UY SF2;

CL = CL1 et CL2 et CL3 et CL4;


************************************
* Chargement
************************************

VAR0 = MANU CHML MOD1 'EEXX' 0. 'EEYY' 0. 'EEZZ' 0. 'EEXY' 0. 
'EEXZ' 0. 'EEYZ' 0. 'p' 0. 'f' 0.0035
'ssta' 0. 'fsta' 0. 'fn' 0. 'fg' 0. 'brok' 0. ;

DEPMAX = 2.5;
TFIN0 = 1000.;
PAS1 = 1.;
PAS2 = 8 * PAS1 ;

LTPC = PROG 0. PAS PAS1 TFIN0;
LTPS = PROG 0. PAS PAS1 TFIN0;
*LTPS = PROG 0. PAS PAS2 TFIN0;


DEP1 = DEPI CL4 1. ; 
EV0  = EVOL MANU TEMPS ( PROG 0. TFIN0 )
                      Y ( PROG 0. DEPMAX ) ; 
CHA0 = CHAR DIMP DEP1 EV0 ;


TAB1 = TABLE;
TAB1.'MOVA' = 'MOT' 'RIEN' ;
* TAB1.'TEMPERATURES' = TABLE ;
TAB1.'VARIABLES_INTERNES' = TABLE ;
TAB1.'BLOCAGES_MECANIQUES' = CL ET REL1 ET REL2;
TAB1.'MODELE' = MOD1;
TAB1.'CARACTERISTIQUES' = MAT1;
TAB1.'CHARGEMENT' = CHA0;
TAB1.'TEMPS_CALCULES' = LTPC;
* TAB1.'TEMPERATURES' . 0 = THE1;
TAB1.VARIABLES_INTERNES.0 = VAR0;
*TAB1.'GRANDS_DEPLACEMENTS' = VRAI;
* TAB1.'GRANDES_DEFORMATIONS' = VRAI;
TAB1.'TEMPS_SAUVES' = LTPS;
TAB1.'LAGRANGIEN' = 'MOT' 'REACTUALISE';
*TAB1.'CONVERGENCE_FORCEE' = FAUX;
TAB1.'CONVERGENCE_FORCEE' = VRAI;

PASAPAS TAB1;

LIST TAB1 ;

TAB2 = TAB1.CONTRAINTES;
TAB3 = TAB1.VARIABLES_INTERNES;
TAB4 = TAB1.DEPLACEMENTS;
TAB5 = TAB1.REACTIONS;

LIST TAB4 ;

NN = DIME (TAB2);
MESS NN;
DEF = PROG;
CON = PROG;
POR = PROG;
DEFP  = PROG;

I = 0;

MAILLAGE = EXTR (TAB1 . WTABLE . 'MOD_MEC') 'MAIL' ;
P6 = MAILLAGE POIN 'PROC' (P6) ;
P5 = MAILLAGE POIN 'PROC' (P5) ;
P7 = MAILLAGE POIN 'PROC' (P7) ;
SH = MAILLAGE POIN PLAN P5 P6 P7 0.0001;
PM = MAILLAGE POIN 'PROC' (PM) ;

REPE FFF NN;

AAA = EXTR TAB2.I SMZZ 1 1 1;
CON = CON ET (PROG AAA);
BBB = EXTR TAB3.I 'p' 1 1 1;
DEF = DEF ET (PROG BBB);

CCC = EXTR TAB3.I 'f' 1 1 1;
POR = POR ET (PROG CCC);

DDD = EXTR TAB3.I 'p' 1 1 1;
DEFP = DEFP ET (PROG DDD);

*rea1 = REDU (TAB1.REACTIONS.I) SH;
*LIST rea1;
*foY = MAXI (EXCO 'FZ' (RESU rea1));
*LF = LF ET (PROG foY);

*DEF1 = DEFO V1 (TAB1.DEPLACEMENTS.I) 1.;
*TRAC V1;
*TRAC DEF1 ;
*TRAC (TAB1.REACTIONS.I) DEF1 ;

I = I+1;

FIN FFF;

*LIST DEP;

EV1 = EVOL BLEU MANU 'DEF' DEF 'CONT' CON;
EV2 = EVOL BLEU MANU 'DEF' DEF 'POROS' POR;
EV3 = EVOL BLEU MANU 'DEFP' DEFP 'CONT' CON;
EV4 = EVOL BLEU MANU 'DEFP' DEFP 'POROS' POR;
EVTOT = EV1 ET EV2 ET EV3 ET EV4 ;


DESS EV1;
DESS EV2;
DESS EV3;
DESS EV4;



OPTI SORT TRACTION_AJUSTE;
SORT EXCE EVTOT ;

LDPT = PROG;
LDPT = INSE LDPT 1 (0.);

LF = PROG ;
LF = INSE LF  1 (0.) ;
NBPAS = DIME (TAB1.TEMPS) - 1;

REPE IND NBPAS;

DPT0 = EXTR TAB1.DEPLACEMENTS.&IND UZ P6;
LDPT = LDPT ET (PROG DPT0);
	
REA1 = REDU (TAB1.REACTIONS.&IND) SH;
FOY = MAXI (EXCO 'FZ' (RESU REA1));
LF = LF ET (PROG FOY);

FIN IND;

E_FORCE = EVOL MANU 'DPT (MM)' LDPT 'FORCE (N)' LF;
DESS E_FORCE TITR 'FORCE(N)- DPT(MM)';

OPTI SORT 'FORCE_DPT.CSV';
SORT EXCE E_FORCE ;

*FIN;
